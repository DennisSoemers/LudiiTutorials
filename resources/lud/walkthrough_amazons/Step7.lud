// Step 7 in the tutorial for writing Amazons in .lud format from scratch
//
// This file improves how Amazons is displayed graphically in Ludii, but is
// otherwise identical to the game description of Step 6.

(game "Amazons"  
    (players 2)  
    (equipment 
        { 
            (board (square 10)) 
            (piece "Queen" Each (slide (then (moveAgain))))
            (piece "Dot" Neutral)
        }
    )  
    (rules 
        (start 
            { 
                (place "Queen1" {"A4" "D1" "G1" "J4"})
                (place "Queen2" {"A7" "D10" "G10" "J7"})
            }
        )
        
        (play 
            (if (is Even (count Moves))
                (forEach Piece)
                (shoot "Dot0")
            )
        )
        
        (end 
           (if 
             (no Moves Next)  
             (result Mover Win) 
           ) 
        )  
    )
)

(metadata 
    (graphics 
        {
            (pieceScale "Dot" 0.333)
            (boardStyle ChessStyle)
        }
    )
)